import org.jetbrains.dokka.gradle.DokkaTask

buildscript {
    apply from: gradle.dolbyio.files.versions
    apply from: gradle.dolbyio.files.modules

    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath dolbyio.android.gradlePlugin
        classpath dolbyio.nexus.gradlePlugin
        classpath dolbyio.kotlin.gradlePlugin
        classpath dolbyio.sonarcube.gradlePlugin
        classpath dolbyio.license.gradlePlugin
        classpath dolbyio.gradle.versions.plugin
        classpath dolbyio.version.catalog.update.plugin
        classpath dolbyio.hilt.gradle
        classpath dolbyio.mkdocs.gradlePlugin
        classpath dolbyio.dokka.gradlePlugin
    }
}

plugins {
    alias(dolbyio.plugins.dokka)
    alias(dolbyio.plugins.publish.nexus)
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
    }
}

subprojects {
    task allDeps(type: DependencyReportTask) {}

    if (isSourcesModule(it)) {
        apply from: gradle.dolbyio.files.moduleSetup
        apply from: gradle.dolbyio.files.ktlint
        apply from: gradle.dolbyio.files.detekt
    }

    tasks.withType(DokkaTask).configureEach {
        dokkaSourceSets.configureEach {
            displayName.set("Android")
            // Disable linking to online kotlin-stdlib documentation
            noStdlibLink.set(false)
            // Disable linking to online JDK documentation
            noJdkLink.set(false)
            // Disable linking to online Android documentation (only applicable for Android projects)
            noAndroidSdkLink.set(false)
            // Used for linking to JDK documentation
            jdkVersion.set(11)
            // Suppress obvious functions like default toString or equals.
            suppressObviousFunctions.set(true)
            // A set of visibility modifiers that should be documented
            documentedVisibilities.set([
                org.jetbrains.dokka.DokkaConfiguration.Visibility.PUBLIC
            ].toSet())
            // Emit warnings about not documented members. Applies globally, also can be overridden by packageOptions
            reportUndocumented.set(false)
            // Suppress a packages
            perPackageOption {
                matchingRegex.set(/org\.webrtc.*/)
                suppress.set(true)
            }
        }
    }
}

ext {
    groupId = "io.dolby"
    version = "0.0.0"
}

apply from: gradle.dolbyio.files.jacocoProject
apply from: gradle.dolbyio.files.dependencyUpdates
