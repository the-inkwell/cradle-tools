pluginManagement {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral() // <- set for the validation pending managed-device plugin
        mavenLocal() // <- set for the validation pending managed-device plugin
    }
}

//in order to change the version used, change this or override DOLBYIO_GRADLE_VERSION_USED (sha, tag, etc...)

def dolbyioGradle = "./dolbyio.gradle"

if (settings.ext.find("DOLBYIO_GRADLE_VERSION_USED")) {
    def version = DOLBYIO_GRADLE_VERSION_USED
    def random = new Random()
    dolbyioGradle = "https://raw.githubusercontent.com/voxeet/voxeet-sdk-android-gradle/${version}/dolbyio.gradle?rand=${random.nextInt()}"
}

apply from: dolbyioGradle

dependencyResolutionManagement {
    versionCatalogs {
        dolbyio {
            from(files(gradle.dolbyio.files.catalog))
        }
    }
}

["library", "app"].each {
    include ":$it"
    project(":$it").projectDir = new File(rootProject.projectDir, "sample/$it")
}

includeBuild "configurations/kt-plugins"